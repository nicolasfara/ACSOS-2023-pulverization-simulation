module org:protelis:dischargeBattery
import it.nicolasfarabegoli.pulverization.interop.ProtelisInterop.onBatteryChangeEvent

/**
 * Given the [currentCharge] the function discharge the battery accordingly.
 * @returns the new charge.
 */
def discharge(currentCharge) {
    let behaviourDischargeRate = env.get("behaviourDischargeRate")
    let communicationDischargeRate = env.get("communicationDischargeRate")
    let sensorsDischargeRate = env.get("sensorsDischargeRate")
    let intraComponentCommCost = env.get("intraComponentCommCost")

    let behaviourInDevice = env.get("behaviourInDevice")
    let communicationInDevice = env.get("communicationInDevice")
    let sensorsInDevice = env.get("sensorsInDevice")

    let intraCommCosts = intraComponentCost(behaviourInDevice, communicationInDevice, sensorsInDevice)
    let behaviourCost = if (behaviourInDevice) { behaviourDischargeRate } else { 0.0 }
    let commCost = if (communicationInDevice) { communicationDischargeRate } else { 0.0 }
    let sensorsCost = if (sensorsInDevice) { sensorsDischargeRate } else { 0.0 }

    let newCharge = currentCharge - intraCommCosts - behaviourCost - commCost - sensorsCost;
    if (newCharge < 0) { env.put("isCharging", true); 0.0 } else { newCharge }
}

/**
 * Determines the communication cost based on the local active components.
 * @returns the cost for the communication between (remote) components.
 */
def intraComponentCost(bhvLocal, commLocal, sensLocal) {
    let intraComponentCommCost = env.get("intraComponentCommCost")
    if (bhvLocal) {
        if (!commLocal) { intraComponentCommCost } else { 0.0 } + if (!sensLocal) { intraComponentCommCost } else { 0.0 }
    } else {
        if (commLocal) { intraComponentCommCost } else { 0.0 } + if (sensLocal) { intraComponentCommCost } else { 0.0 }
    }
}

def recharge(currentCharge) {
    if (currentCharge >= 100.0) { env.put("isCharging", false); 100.0 } else { currentCharge + 1.0 }
}

let now = self.getCurrentTime()
let isCharging = env.get("isCharging")
let newCharge = if (isCharging) { recharge(env.get("battery")) } else { discharge(env.get("battery")) }
env.put("battery", newCharge)
onBatteryChangeEvent()
